// ========================================
// Intents system to allow class-agnostic
// communication between modules
// ========================================
extend class UaS {
	array<UaS_Intent> IntentsList;
	int debug_sent, debug_got, debug_cleaned, debug_arraycycles;

	static play void SendIntent(
		string Intent,
		string Payload,
		object Sender,
		object Receiver = null,
		int Timeout = 5)
	{
		UaS_Intent NewIntent = new("UaS_Intent");
		NewIntent.Intent = Intent;
		NewIntent.Payload = Payload;
		NewIntent.Sender = Sender;
		NewIntent.Receiver = Receiver;
		NewIntent.Timeout = Timeout;
		NewIntent.Created = level.maptime;
		let Handler = UaS(EventHandler.Find('UaS')); // Thanks Ace!
		Handler.IntentsList.Push(NewIntent);
		Handler.debug_sent++;
	}

	static play UaS_Intent GetIntent(
		string Intent,
		object Sender = null,
		object Receiver = null)
	{
		let Handler = UaS(EventHandler.Find('UaS'));
		bool matchSender   = (Sender   != null);
		bool matchReceiver = (Receiver != null);
		for (int i = 0; i < Handler.IntentsList.size(); i++) {
			handler.debug_arraycycles++;
			UaS_Intent ci = Handler.IntentsList[i];
			// Skip null, cleared, or expired intents
			if (!ci || ci.toDestroy || (level.maptime - ci.Created > ci.Timeout)) { continue; }
			// Skip non-matching sender/receiver if applicable
			if (matchSender && ci.Sender != Sender) { continue; }
			if (matchReceiver && ci.Receiver != Receiver) { continue; }
			// Match main criteria
			if (ci.Intent == Intent) { Handler.debug_got++; return ci; }
		}
		return null; // nothing found
	}

	void Cleanup_Intents() {
		// Iterate over intents list
		for (int i = 0; i < IntentsList.size(); i++) {
			debug_arraycycles++;
			UaS_Intent ci = IntentsList[i];
			// Dereference any intents that got destroyed somehow
			if (!ci) {
				IntentsList.delete(i);
				debug_cleaned++;
				continue;
			}
			// Delete any latent or flagged intents as we go
			if (ci.toDestroy || (level.maptime - ci.Created > ci.Timeout)) {
				IntentsList.delete(i);
				ci.Destroy();
				debug_cleaned++;
				continue;
			}
		}
	}

	void IntentTick() {
		Cleanup_Intents();
		if (level.maptime % 175 == 0) {
			UaS.LogDebug(
				DBG_Intents,
				string.format("Intents 5 seconds: %i sent, %i retrieved, %i cleaned, %i array cycles, %i pending",
					debug_sent, debug_got, debug_cleaned, debug_arraycycles, IntentsList.size()));
			debug_sent = 0;
			debug_got = 0;
			debug_cleaned = 0;
			debug_arraycycles = 0;
		}
	}

	// static play void PrintIntentsList() {
		// let Handler = UaS(EventHandler.Find('UaS'));
		// for (int i = 0; i < Handler.IntentsList.size(); i++) {
			// console.printf("%i %s",i,Handler.IntentsList[i].Intent);
		// }
	// }

	// debug tests
	// override void WorldTick() {
		// if (level.maptime % 70 == 0) { UaS.PrintIntentsList(); }
	// }
}

class UaS_Intent {
	string Intent;
	string Payload;
	object Sender;
	object Receiver;
	int Timeout;
	int Created;
	bool toDestroy;

	void Clear() {
		toDestroy = true;
	}
}
